protocol analyzer spicy::RESP over TCP:
    parse originator with RESP::ClientMessages,
    parse responder with RESP::ServerMessages,
    port 6379/tcp;

import RESP;
import Zeek_RESP;

# TODO: These should be a bit nicer with how they make commands, lotta wasted work
on RESP::ClientData if ( Zeek_RESP::is_set(self) ) -> event RESP::set_command($conn, $is_orig, Zeek_RESP::make_set(Zeek_RESP::make_command(self)));
on RESP::ClientData if ( Zeek_RESP::is_get(self) ) -> event RESP::get_command($conn, $is_orig, Zeek_RESP::make_get(Zeek_RESP::make_command(self)));

# All client data is a command
on RESP::ClientData -> event RESP::command($conn, $is_orig, Zeek_RESP::make_command(self));
